.set CODE, 0x00000000
.set XOS_Module, 0x2001E
.set XOS_ValidateAddress, 0x2003A
.set XOS_CallEvery, 0x2003C
.set XOS_RemoveTickerEvent, 0x2003D
.set XEconet_StartTransmit, 0x60006
.set XEconet_AbandonTransmit, 0x60008
.set scorePort, 0x00
.set scoreStation, 0x04
.set scoreNetwork, 0x08
.set currentRank, 0x0C
.set netTally, 0x10
.set netDeaths, 0x14
.set econetHandle, 0x18
.set semaphore, 0x30
.set transmitBuffer, 0x20
.set trName, 0x20
.set trLegal, 0x28
.set trCondition, 0x29
.set trKill, 0x2A
.set trDeaths, 0x2B
.set trCredits, 0x2C
.set trMachine, 0x30
.set endOfBuffer, 0x34
.org CODE
.long    0
.long    InitialiseModule
.long    FinaliseModule
.long    0
.long    title
.long    help
.long    0
title:
.byte    "EliteScoreboard"
.byte    0
.balign 4
help:
.byte    "EliteScoreboard"
.byte    9
.byte    "1.00 (26 Mar 2025)"
.byte    0
.balign 4
InitialiseModule:
 STMFD   R13!, {R14}
 LDR     R2, [R12]
 TEQ     R2, #0
 BNE     init1
 MOV     R0, #6
 MOV     R3, #0x40
 SWI     XOS_Module
 LDMVSFD R13!, {PC}
 STR     R2, [R12]
 MOV     R1, #0
 STR     R1, [R2, #econetHandle]
 STR     R1, [R2, #netTally]
 STR     R1, [R2, #netDeaths]
 STR     R1, [R2, #currentRank]
 STR     R1, [R2, #scoreNetwork]
 STR     R1, [R2, #scoreStation]
 STR     R1, [R2, #scorePort]
 STR     R1, [R2, #scoreNetwork]
 MOV     R1, #100
 STR     R1, [R2, #scorePort]
 MOV     R1, #128
 STR     R1, [R2, #scoreStation]
init1:
 MOV     R0, #100
 ADR     R1, TransmitData
 SWI     XOS_CallEvery
 LDMFD   R13!, {PC}^
FinaliseModule:
 STMFD   R13!, {R14}
 LDR     R12, [R12]
 ADR     R0, TransmitData
 MOV     R1, R12
 SWI     XOS_RemoveTickerEvent
 LDR     R0, [R12, #econetHandle]
 TEQ     R0, #0
 SWINE   XEconet_AbandonTransmit
 MOV     R1, #0
 STR     R1, [R12, #econetHandle]
 LDMFD   R13!, {PC}^
TransmitData:
 STMFD   R13!, {R0-R12}
 MOV     R5, PC
 BIC     R4, R5, #3
 TEQP    R4, #3
 MOV     R0, R0
 STMFD   R13!, {R5, R14}
 LDR     R0, string1Addr
 MOV     R1, R0
 SWI     XOS_ValidateAddress
 BVS     done
 BCS     done
 LDR     R0, [R0]
 LDR     R1, string1Match
 CMP     R0, R1
 BNE     done
 LDR     R0, string2Addr
 MOV     R1, R0
 SWI     XOS_ValidateAddress
 BVS     done
 BCS     done
 LDR     R0, [R0]
 LDR     R1, string2Match
 CMP     R0, R1
 BNE     done
 LDR     R0, string3Addr
 MOV     R1, R0
 SWI     XOS_ValidateAddress
 BVS     done
 BCS     done
 LDR     R0, [R0]
 LDR     R1, string3Match
 CMP     R0, R1
 BNE     done
 LDR     R0, [R12, #semaphore]
 TEQ     R0, #0
 BNE     return
 MOV     R0, #1
 STR     R0, [R12, #semaphore]
 LDR     R0, [R12, #econetHandle]
 TEQ     R0, #0
 SWINE   XEconet_AbandonTransmit
 LDR     R0, cmdrName
 ADD     R1, R0, #4
 SWI     XOS_ValidateAddress
 BVS     done
 BCS     done
 LDR     R2, [R0]
 STR     R2, [R12, #trName]
 LDR     R2, [R0, #4]
 STR     R2, [R12, #trName+4]
 MOV     R2, #13
 STRB    R2, [R12, #trName+7]
 LDRB    R3, [R12, #trName+6]
 TEQ     R3, #0
 STREQB  R2, [R12, #trName+6]
 LDRB    R3, [R12, #trName+5]
 TEQ     R3, #0
 STREQB  R2, [R12, #trName+5]
 LDRB    R3, [R12, #trName+4]
 TEQ     R3, #0
 STREQB  R2, [R12, #trName+4]
 LDRB    R3, [R12, #trName+3]
 TEQ     R3, #0
 STREQB  R2, [R12, #trName+3]
 LDRB    R3, [R12, #trName+2]
 TEQ     R3, #0
 STREQB  R2, [R12, #trName+2]
 LDRB    R3, [R12, #trName+1]
 TEQ     R3, #0
 STREQB  R2, [R12, #trName+1]
 LDRB    R3, [R12, #trName]
 TEQ     R3, #0
 STREQB  R2, [R12, #trName]
 MOV     R3, #1
 LDR     R0, cmdrLegal
 MOV     R1, R0
 SWI     XOS_ValidateAddress
 BVS     done
 BCS     done
 LDR     R2, [R0]
 CMP     R2, #0x28
 MOVLE   R3, #0
 CMP     R2, #0x3E8
 MOVGT   R3, #3
 STRB    R3, [R12, #trLegal]
 LDR     R0, cmdrCondition
 MOV     R1, R0
 SWI     XOS_ValidateAddress
 BVS     done
 BCS     done
 LDR     R2, [R0]
 ADD     R2, R2, #1
 CMP     R2, #4
 CMPNE   R2, #5
 MOVEQ   R2, #3
 CMP     R2, #6
 MOVGE   R2, #0
 STRB    R2, [R12, #trCondition]
 LDRB    R2, [R12, #netTally]
 STRB    R2, [R12, #trKill]
 LDRB    R2, [R12, #netDeaths]
 STRB    R2, [R12, #trDeaths]
 LDR     R0, cmdrCredits
 MOV     R1, R0
 SWI     XOS_ValidateAddress
 BVS     done
 BCS     done
 LDR     R2, [R0]
 STR     R2, [R12, #trCredits]
 MOV     R2, #4
 STR     R2, [R12, #trMachine]
 LDR     R1, [R12, #scorePort]
 TEQ     R1, #0
 LDRNE   R2, [R12, #scoreStation]
 TEQNE   R2, #0
 BEQ     done
 MOV     R0, #0
 LDR     R3, [R12, #scoreNetwork]
 ADD     R4, R12, #transmitBuffer
 MOV     R5, #20
 MOV     R6, #1
 MOV     R7, #0
 SWI     XEconet_StartTransmit
 MOVVS   R0, #0
 STR     R0, [R12, #econetHandle]
done:
 MOV     R0, #0
 STR     R0, [R12, #semaphore]
return:
 LDMFD   R13!, {R5, R14}
 TEQP    R5, #0
 MOV     R0, R0
 LDMFD   R13!, {R0-R12}
 MOV     PC, R14
cmdrName:
.long    0x0005A698
cmdrRank:
.long    0x0005A960
cmdrCredits:
.long    0x0006534C
cmdrLegal:
.long    0x00065398
cmdrCondition:
.long    0x000653A0
string1Addr:
.long    0x0001AF14
string1Match:
.long    0x4E495247
string2Addr:
.long    0x0001AF00
string2Match:
.long    0x45525241
string3Addr:
.long    0x00071508
string3Match:
.long    0x34312E31
